// gradle clean build

plugins {
    id 'java'
}

apply plugin : 'eclipse'

repositories {
    mavenCentral()
}

sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

compileJava11Java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

jar {
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }
    manifest { 
        attributes  'Automatic-Module-Name': 'id.xfunction',
            'Multi-Release': 'true' 
    }
}

task packageJavadoc(type: Zip) {
    archiveFileName = "xfunction-javadoc.zip"
    destinationDirectory = file("$projectDir/release")
    from file("$projectDir/build/docs")
}

task createRelease(type: Copy) {
    def out = 'release'
    from jar.archiveFile
    into out
}

task cleanRelease(type: Delete) {
    delete "$projectDir/release"
}

assemble.dependsOn createRelease
createRelease.dependsOn javadoc,packageJavadoc
clean.dependsOn cleanRelease
