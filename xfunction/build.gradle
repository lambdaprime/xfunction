plugins {
  id 'com.android.library' version '8.2.0' apply false
}

apply plugin: 'com.android.library'

def androidSdkVersion = 34
def javaVersion = 17

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(javaVersion)
  }
}

android {
  namespace 'id.xfunction'
  compileSdk androidSdkVersion
  compileOptions {
    sourceCompatibility javaVersion
  }
  lintOptions {
    abortOnError false
  }
  defaultConfig {
    targetSdkVersion androidSdkVersion
    minSdkVersion androidSdkVersion
  }
}

task createRelease(type: Copy, dependsOn: 'bundleReleaseAar') {
  from "${buildDir}/outputs/aar/${rootProject.name}-release.aar"
  into "${projectDir}/release"
  rename "${rootProject.name}-release.aar", "${rootProject.name}-${version}.aar"
}

task cleanRelease(type: Delete) {
  delete "$projectDir/release/*-SNAPSHOT.jar"
}

assemble.dependsOn createRelease
clean.dependsOn cleanRelease

if (project.hasProperty('publish')) {

  // Initially it was taken from https://central.sonatype.org/publish/publish-gradle/
  // but since Gradle removed this plugin https://docs.gradle.org/7.0/userguide/upgrading_version_6.html#removal_of_the_legacy_maven_plugin
  // it was rewritten for new one

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'id.xfunction'
        groupId = 'io.github.lambdaprime'
        artifact source: "${buildDir}/outputs/aar/${rootProject.name}-release.aar", classifier: 'android'
        pom {
          name = 'id.xfunction'
          description = 'Java library which provides set of extensions for standard Java APIs'
          url = 'https://github.com/lambdaprime/xfunction'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'lambdaprime'
              email = 'intid@protonmail.com'
            }
          }

          scm {
            url = 'https://github.com/lambdaprime/xfunction.git'
          }
        }
      }
    }
    repositories {
      maven {
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
    }
  }
}
